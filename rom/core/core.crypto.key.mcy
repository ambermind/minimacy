/* Copyright (c) 2022, Sylvain Huet, Ambermind
   This program is free software: you can redistribute it and/or modify it
   under the terms of the GNU General Public License, version 2.0, as
   published by the Free Software Foundation.
   This program is distributed in the hope that it will be useful, but WITHOUT
   ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
   FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License,
   version 2.0, for more details.
   You should have received a copy of the GNU General Public License along
   with this program. If not, see <https://www.gnu.org/licenses/>. */

use core.crypto.asn1;;
use core.crypto.rsa;;
use core.crypto.oid;;
use core.crypto.pem;;

//-------------RSA-----------------------------
fun _rsaFromAsn1 asn1=
	let listFromAsn1 asn1 -> content in
	if 9==listLength content then
	let content -> version:numbers in
	if 0==intFromAsn1 version then
	let hashmapCreate 4 -> h in
	let OID_MODULUS:OID_PUBEXP:"PRIVEXP":"P1":"P2":"E1":"E2":"C":nil -> fields in
	(
		hashmapSet h "ENCRYPTION" OID_rsaEncryption;
		for v in asn1ListOfHex numbers do 
		(
			hashmapSet h (head fields) v;
			set fields=tail fields
		);
		h
	);;

fun _ecFromAsn1 asn1=
	let listFromAsn1 asn1 -> content in
	if 4<=listLength content then
	let content ->  version:priv:curve:pub:_ in
	if 1==intFromAsn1 version then
	let hashmapCreate 4 -> h in
	let listFromAsn1 asn1 -> _:priv:curve:pub:_ in (
		hashmapSet h "ENCRYPTION" OID_ecPublicKey;
		hashmapSet h "CURVE" strFromAsn1 head listFromAsn1 curve;
		hashmapSet h "PRIVKEY" hexFromStr strFromAsn1 priv;
		hashmapSet h "PUBKEY" hexFromStr strFromAsn1 head listFromAsn1 pub;
		h
	);;

fun _keyFromAsn1 asn1=
//	asn1Echo "" asn1;
	let listFromAsn1 asn1 ->(version:encryption:raw:_) in
	if 0==intFromAsn1 version then
	let strFromAsn1 head listFromAsn1 encryption -> encryption in 
	match encryption with
		OID_rsaEncryption -> _rsaFromAsn1 head asn1Unpack strFromAsn1 raw,
		OID_ecPublicKey -> _ecFromAsn1 head asn1Unpack strFromAsn1 raw;;

fun keyFromDER der= derProcess der #_keyFromAsn1;;
fun rsaFromDER der= derProcess der #_rsaFromAsn1;;
fun ecFromDER der= derProcess der #_ecFromAsn1;;

fun keyFromPEM pem pwd=
	let head pemDecode pem pwd ->[name der] in
	match name with
		"PRIVATE KEY"->keyFromDER der,
		"RSA PRIVATE KEY"->rsaFromDER der,
		"EC PRIVATE KEY"->ecFromDER der;;

fun rsaFromPEM pem pwd= if pem<>nil then rsaImport keyFromPEM pem pwd;;
