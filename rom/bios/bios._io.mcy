/* Copyright (c) 2022, Sylvain Huet, Ambermind
   This program is free software: you can redistribute it and/or modify it
   under the terms of the GNU General Public License, version 2.0, as
   published by the Free Software Foundation.
   This program is distributed in the hope that it will be useful, but WITHOUT
   ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or
   FITNESS FOR A PARTICULAR PURPOSE. See the GNU General Public License,
   version 2.0, for more details.
   You should have received a copy of the GNU General Public License along
   with this program. If not, see <https://www.gnu.org/licenses/>. */

//-------------- IO check
var _Streams;;

// alive if at least one stream different from _InteralPipe
// (tail _Streams<>nil || head _Stream<>_internalPipe)
fun _streamAlive = (tail _Streams)<>nil;;		// there is always the internal stream
fun streamCheck s=
	if s._thS._stateT <> _THREAD_DONE then true
	else (
		streamClose s;
		false
	);;
fun streamSocket s=s._socketS;;

fun streamOnSelectRead s fRead=
	if streamCheck s then threadPost s._thS (lambda =
		_socketSetSelectRead s._socketS true;
		call fRead s._onReadS
	);
	true;;

fun streamOnSelectWrite s =
	threadPost s._thS s._onWriteS;
	true;;

fun _streamClean= \atomic
	let _Streams -> streams in (
	set _Streams=nil;
	let listFilter streams #streamCheck -> streams in
	set _Streams=listConcat _Streams streams
);;

fun _schedulerInitIO =
	_internalInit;
	true;;

fun streamCommonClose s=
//	echoLn "]=== _socketClose"; dumpCallstack nil;
	set s._socketS=nil;
	set s._thS=nil;
	set s._onReadS=nil;
	set s._onWriteS=nil;
	set _Streams= listRemove _Streams Stream<s;
	true;;

fun streamClose s= call s._closeS;;

fun streamWrite s data start =
//	echoLn strFormat "]=== streamWrite * *" start strLength data; dump
	let call s._writeS data start -> next in
	let strLength data -> len in (
		_socketSetSelectWrite s._socketS next<>nil && next<>len;
		socketSetWritable s._socketS next==len;
		next
	);;

fun _streamAddToList s=
	let Stream<s -> s in
	\atomic
		if !listContains _Streams s then (
			set _Streams=s:_Streams
		)
	;;
fun _streamRemoveFromList s=
	let Stream<s -> s in
	\atomic 
		set _Streams=listRemove _Streams s
	;;
fun _streamOnRead s onRead =
	set s._thS= _This;
	set s._onReadS= if onRead<>nil then (lambda data= 
//		echoLn strFormat "-> onRead * bytes" strLength data; hexDump data;
		call onRead data;
		0
	);
	if onRead<>nil then _streamAddToList s;
	_socketSetSelectRead s._socketS onRead<>nil;
	s;;

fun _streamOnWrite s onWrite =
	set s._thS= _This;
	set s._onWriteS= if onWrite<>nil then (lambda = call onWrite; 0);
	if onWrite<>nil then _streamAddToList s;
	s;;
fun streamSetSelectWrite s value =	_socketSetSelectWrite s._socketS value&&(s._onWriteS<>nil);;
fun streamSetSelectRead s value =	_socketSetSelectRead s._socketS value&&(s._onReadS<>nil);;
fun streamSetReadable s value = socketSetReadable s._socketS value;;
fun streamSetWritable s value = socketSetWritable s._socketS value;;

fun streamOnEvent s onRead onWrite=
	_streamOnRead s onRead;
	_streamOnWrite s onWrite;
	if onWrite<>nil then streamSetSelectWrite s true;
	s;;

fun streamInit s socket onSelectRead onSelectWrite write close show=
	set s._socketS=socket;
	set s._onSelectReadS=onSelectRead;
	set s._onSelectWriteS=onSelectWrite;
	set s._writeS=write;
	set s._closeS=close;
	set s._showS=show;
	s;;

fun _bestTimeout current next=
	if next==nil then current
	else if current==nil then next
	else let next-current -> delta in
	if delta<0 then next else current;;

fun _streamPrepareSelect fifoSelect =
	_streamClean;
	let nil -> timeout in (
		for s in _Streams do (
			if !_threadFull s._thS then (
				if (_socketSelectWrite s._socketS) && (_socketWritable s._socketS) then set timeout=timeMs;
				if (_socketSelectRead s._socketS) && (_socketReadable s._socketS) then set timeout=timeMs;
				if (_socketSelectWrite s._socketS)||(_socketSelectRead s._socketS) then (
//					dump s._socketS;
//					dump _socketSelectWrite s._socketS;
//					dump _socketSelectRead s._socketS;
					fifoIn fifoSelect s
				)
			)
		);
//		dump fifoCount fifoSelect;
		timeout
	);;

fun _ioCheck=
//	echoTime ["nbRun= " fifoCount _RunList];
	let timeMs+_SELECT_TIMEOUT -> timeout in
	let _bestTimeout timeout _timerTimeout -> timeout in
	let fifoCreate -> fifoSelect in
	let _bestTimeout timeout _streamPrepareSelect fifoSelect -> timeout in
	let _bestTimeout timeout if 0<>fifoCount _RunList then timeMs -> timeout in
	let max 0 timeout-timeMs -> wait in
	let if _isAlive then wait else 0 -> wait in 
	let fifoList fifoSelect -> lSelect in
	let listMap lSelect (lambda s= s._socketS) -> lSockets in
//	let max 1000 wait -> wait in
	(	

//		echo ".";
//		echoTime strFormat "nbStreams=* nbSocks=*  timeout=*" (listLength _Streams) (listLength lSockets) wait;
		_select lSockets wait;
		for s in lSelect do (
/*			if _socketWritable s._socketS then (
				echoLn "]=== _socketWritable"; // dump s
			);
			if _socketReadable s._socketS then (
				echoLn "]=== _socketReadable"; // dump s
			);
*/			if (_socketSelectWrite s._socketS) && (_socketWritable s._socketS) then (
				_socketSetSelectWrite s._socketS false;	// we remove it until the next uncomplete write
				call s._onSelectWriteS
			);
			if (_socketSelectRead s._socketS) && (_socketReadable s._socketS) then (
				_socketSetSelectRead s._socketS false;	// we remove it until the socket is read
				call s._onSelectReadS
			);
		);
		_workerCheck;
		_timerCheck
	);;


fun watcherUpdate watcher addr mask value=
	_watcherUpdate watcher._socketS addr mask value;
	watcher;;

fun watcherOpen name=
	let [Stream] -> stream in
	streamInit stream _watcherOpen
		(lambda = streamOnSelectRead stream (lambda cbRead= call cbRead "x"))	// onSelectRead
		nil	// onSelectWrite
		nil	// write
		nil	// close
		(lambda fEcho= call fEcho "watcher" name);;	//show

fun watcherOnEvent watcher onRead = streamOnEvent watcher (lambda data=call onRead; nil) nil;;
fun watcherClose watcher = streamClose watcher;;